apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

android {
    compileSdkVersion 12
    buildToolsVersion "20.0.0"

    defaultConfig {
        minSdkVersion 12
        targetSdkVersion 12
        versionCode 2
        versionName "0.2.1"
    }

    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

dependencies {
    androidTestCompile 'org.mockito:mockito-core:1.9.5'
    androidTestCompile 'com.google.dexmaker:dexmaker:1.0'
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.0'
}

// Upload AAR according to Sonatype rules
uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: sonatypeStaging) {
            authentication(
                userName: sonatypeUsername,
                password: sonatypePassword
            )
        }
        snapshotRepository(url: sonatypeSnapshot) {
            authentication(
                userName: sonatypeUsername,
                password: sonatypePassword
            )
        }

        pom.groupId = GROUP
        pom.artifactId = POM_ARTIFACT_ID
        pom.version = VERSION_NAME

        pom.project {
            name POM_NAME
            packaging POM_PACKAGING
            description POM_DESCRIPTION
            url POM_URL

            scm {
                url POM_SCM_URL
                connection POM_SCM_CONNECTION
                developerConnection POM_SCM_DEV_CONNECTION
            }

            licenses {
                license {
                    name POM_LICENSE_NAME
                    url POM_LICENSE_URL
                    distribution POM_LICENSE_DIST
                }
            }

            developers {
                developer {
                    id POM_DEVELOPER_ID
                    name POM_DEVELOPER_NAME
                }
            }
        }
    }
}

// Signing
signing {
    required { has("release") && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

// Javadocs and sources
task androidJavadocs(type: Javadoc) {
    options {
        linksOffline "http://d.android.com/reference", "${android.sdkDirectory}/docs/reference"
    }
    exclude '**/BuildConfig.java'
    exclude '**/R.java'
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(project.android.getBootClasspath().join(File.pathSeparator))
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}

// Create a JAR library for retro compatibility with Eclipse IDE
task assembleJar(type: Copy, dependsOn: ['clean', 'assembleRelease']) {
    assembleRelease.mustRunAfter clean

    from('build/bundles/release/')
    into('build/libs/')
    include('classes.jar')
    rename ('classes.jar', POM_ARTIFACT_ID + '-' + VERSION_NAME + '.jar')
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}
